// BOSS Satellite Boot Script
// Kevin Gisi
// http://youtube.com/gisikw

SET SHIP:CONTROL:PILOTMAINTHROTTLE TO 0.
WAIT UNTIL SHIP:PARTSTAGGED("BOSSLauncher"):LENGTH = 0.

IF SHIP:OBT:SEMIMAJORAXIS < 700000 + SHIP:OBT:BODY:RADIUS {
  LOCK STEERING TO PROGRADE.
  WAIT 10.
  LOCK THROTTLE TO 1 - (0.999 * SHIP:OBT:SEMIMAJORAXIS/(800000 + SHIP:OBT:BODY:RADIUS)).
  WAIT UNTIL SHIP:OBT:SEMIMAJORAXIS >= (800000 + SHIP:OBT:BODY:RADIUS).
  LOCK THROTTLE TO 0.
}

SET p TO SHIP:PARTSTITLED("Communotron 16")[0].
SET m TO p:GETMODULE("ModuleRTAntenna").
IF m:GETFIELD("status") = "OFF" {
  m:DOEVENT("Activate").
}
LIGHTS ON.

WAIT UNTIL ADDONS:RT:HASCONNECTION(SHIP).
SWITCH TO 0.
LIST FILES in allFiles.
FOR file in allFiles {
  IF file:NAME = CORE:PART:TAG + ".init.ks" {
    COPY CORE:PART:TAG + ".init.ks" TO 1.
    SWITCH TO 1.
    RENAME CORE:PART:TAG + ".init.ks" TO "init.ks".
    RUN init.ks.
  }
}

WAIT 10.
REBOOT.
